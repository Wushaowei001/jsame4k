apply plugin: 'java'

sourceCompatibility = 1.5
targetCompatibility = 1.5

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/libs-release" }
    }
    ext {
        appName = "jsame4k-applet"
        appVersion = "1.0.0"
        finalName = "${appName}-${appVersion}"
        proguardBase = "${appName}-proguard-base"
    }
    dependencies {
        classpath "net.sf.proguard:proguard-gradle:4.10"
    }
}

jar {
    baseName = proguardBase
    exclude("jsame-applet.html")
}

task proguard(type: proguard.gradle.ProGuardTask) {
    injars "${buildDir}/libs/${proguardBase}.jar"
    outjars "${buildDir}/libs/${finalName}.jar"
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    keep 'public class S'
}

task deleteProguardBase(type: Delete) {
    delete "${buildDir}/libs/${proguardBase}.jar"
}

task copyAppletHtml(type: Copy) {
    from("src/main/resources")
    into "${buildDir}/libs"
    rename 'jsame-applet.html', "${finalName}.html"
    filter { line -> line.contains('jsame4k-applet.jar') ? line.replaceAll('jsame4k-applet.jar', "${finalName}.jar") : line }
}

deleteProguardBase.dependsOn proguard
build.dependsOn deleteProguardBase
build.dependsOn copyAppletHtml
